#
# CMake build for PyOpenFLUID
#
# Author : Bastien VAYSSE <bastien.vaysse@supagro.inra.fr>
#
CMAKE_MINIMUM_REQUIRED(VERSION 2.6)

PROJECT(PyOpenFluid)

#
# Libraries
#
FIND_PACKAGE(PythonInterp)
FIND_PACKAGE(PythonLibs)
FIND_PACKAGE(Doxygen)
FIND_PACKAGE(OpenFLUID REQUIRED core base ware tools machine fluidx)
FIND_PACKAGE(Boost 1.35.0 REQUIRED COMPONENTS python regex system filesystem)

FIND_PACKAGE(PkgConfig REQUIRED)
PKG_CHECK_MODULES(LibXML2 REQUIRED libxml-2.0)
PKG_CHECK_MODULES(GLIBMM REQUIRED glibmm-2.4)
#PKG_CHECK_MODULES(GTHREAD REQUIRED gthread-2.0)

INCLUDE_DIRECTORIES(${Boost_INCLUDE_DIRS} ${PYTHON_INCLUDE_DIRS})
INCLUDE_DIRECTORIES(${GLIBMM_INCLUDE_DIRS} ${GTHREAD_INCLUDE_DIRS} ${LibXML2_INCLUDE_DIRS})

# checking python version
IF(NOT ${PYTHON_VERSION_MAJOR} EQUAL 2)
  MESSAGE(FATAL_ERROR "Python version must be 2.X, not ${PYTHON_VERSION_MAJOR}.")
ENDIF(NOT ${PYTHON_VERSION_MAJOR} EQUAL 2)

#
# Directories
#
INCLUDE(CMake.in.cmake)
IF (EXISTS ${CMAKE_SOURCE_DIR}/CMake.in.local.cmake)
  INCLUDE(CMake.in.local.cmake)
ENDIF (EXISTS ${CMAKE_SOURCE_DIR}/CMake.in.local.cmake)

SET(PYTHON_OUTPUT_PATH             "${OUTPUT_PATH}/${PYTHON_MODULE_PATH}")
SET(PYTHON_OUTPUT_SRC_PATH         "${OUTPUT_PATH}/${PYTHON_MODULE_SRC_PATH}")
SET(DOC_OUTPUT_PATH                "${BUILD_PATH}/${DOC_PATH}")
SET(TEST_OUTPUT_PATH               "${OUTPUT_PATH}/${TESTS_OUTPUTS_PATH}")

#
# Repositories
#
FILE(MAKE_DIRECTORY "${PYTHON_OUTPUT_PATH}")
FILE(MAKE_DIRECTORY "${PYTHON_OUTPUT_SRC_PATH}")
FILE(MAKE_DIRECTORY "${DOC_OUTPUT_PATH}")
FILE(MAKE_DIRECTORY "${TEST_OUTPUT_PATH}")

#
# Preparation
#
SET(CMAKE_CXX_FLAGS "-Wall")
INCLUDE(PreparationDocCMake.cmake)

#
# Browsing
#
ADD_SUBDIRECTORY(src)
ADD_SUBDIRECTORY(module)

#
# Documentation
#
ADD_SUBDIRECTORY(doc)

#
# Testing
#
IF(NOT EXISTS "${OPENFLUID_TESTS_PROJECT_1}")
  MESSAGE(FATAL_ERROR "Missing dataset (required for tests)")
ELSEIF(NOT IS_DIRECTORY "${OPENFLUID_TESTS_PROJECT_1}")
  MESSAGE(FATAL_ERROR "Wrong dataset path (required for tests)")
ENDIF(NOT EXISTS "${OPENFLUID_TESTS_PROJECT_1}")
ENABLE_TESTING()
ADD_SUBDIRECTORY(tests)

#
# Text informations
#
FILE(COPY "README" DESTINATION "${OUTPUT_PATH}")
FILE(COPY "COPYING" DESTINATION "${OUTPUT_PATH}")
FILE(COPY "INSTALL" DESTINATION "${OUTPUT_PATH}")
if (NOT UNIX)
  FILE(RENAME "${OUTPUT_PATH}/README" "${OUTPUT_PATH}/Readme.txt")
  FILE(RENAME "${OUTPUT_PATH}/COPYING" "${OUTPUT_PATH}/CopyRight.txt")
  FILE(RENAME "${OUTPUT_PATH}/INSTALL" "${OUTPUT_PATH}/Install.txt")
endif (NOT UNIX)
